buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "nz.net.speakman.destinyraidtimers"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 5
        versionName "1.1-beta"
    }

    signingConfigs {
        final Console console = System.console();
        release {
            if (console != null) {
                // 'gradlew build' in the terminal
                storeFile file(console.readLine("\n\$ Enter keystore path: "))
                storePassword new String(console.readPassword("\n\$ Enter keystore password: "))
                keyAlias console.readLine("\n\$ Enter key alias: ")
                keyPassword new String(console.readPassword("\n\$ Enter key password: "))
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }
        debugAnalytics {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.1.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.jakewharton.timber:timber:2.5.1'
    compile 'com.jakewharton:butterknife:5.1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.4@aar') {
        transitive = true;
    }
    compile 'com.sefford:circular-progress-drawable:1.31@aar'
    compile 'com.getbase:floatingactionbutton:1.8.0@aar'
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        // exclusion is not necessary, but generally a good idea.
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile 'com.github.gabrielemariotti.changeloglib:library:1.5.2'
}
